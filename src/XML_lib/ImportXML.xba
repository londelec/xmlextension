<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="ImportXML" script:language="StarBasic">REM  *****  BASIC  *****
Option Explicit


Global GemptyPlhdr As Integer	&apos;Use empty placeholder #EMPTY# when importing a file
Private Const MNAME As String = &quot;Import XML&quot;

Private ColumnNames() As Variant
Private ColumnsOK As Boolean
Private nIO(3) As Long &apos;Number of used nodes for each non-empty IO node type
Private Rpos(3) As Long	&apos;Current row position for writing
Private sheetsIO(3) As Object &apos;Sheets for each IO node type
Private oSheetObj &apos;Spreadheet object
Private XMLn As String &apos;XML file name without path, used for dialogs
Private IOtypes() As Variant &apos;Array (&quot;DI&quot;, &quot;AI&quot;, &quot;DO&quot;, &quot;AO&quot;)
Private oCheckBox(3) &apos;Dialog elements
Private oNameExistsLabel &apos;&quot;Name exists&quot; label object
Private Overwrite As Boolean &apos;Should existing sheets be overwritten?
Private oOWCheckBox &apos;&quot;Overwrite&quot; checkbox object
Private oOKbutt &apos;&quot;OK&quot; button object
Private oSheetName &apos;Sheet name prefix
Private oDialogLib &apos;Dialog library


REM Returns True if sheet exists
Private Function SheetExists(sName As String) As Boolean
	SheetExists = oSheetObj.Sheets.hasByName(sName)
End Function

REM Advanced button click event
Function AdvancedEvent(oEvent)
	Dim oModule, oDlg, oHoffs, oemptyPl
	Dim val as Integer
	oModule = oDialogLib.getByName(&quot;ImportAdv&quot;)
	oDlg = CreateUnoDialog(oModule)
	oHoffs = oDlg.GetControl(&quot;OffsetHText&quot;)
	oHoffs.Text = GfirstCol
	oemptyPl = oDlg.GetControl(&quot;ReplaceEmptyCheckBox&quot;)
	oemptyPl.State = GemptyPlhdr
	DialogSetPosition(oDlg, oEvent.Source.Context, 1)
	If oDlg.execute = 0 Then
		Exit Function
	End If
	val = oHoffs.Text
	If val &gt; 0 Then
		GfirstCol = val
	End If
	GemptyPlhdr = oemptyPl.State
End Function

REM Dialog element event handler
Sub UpdDialog(oEvent)
	Dim i As Integer
	Dim nCheck As Integer &apos;Number of selected nodes
	Dim sName As String &apos;IO sheet name
	Dim ShExists As Boolean
	For i = 0 To 3
		If oCheckBox(i).State = 1 Then nCheck = nCheck + 1 &apos;Count the number of selected sheets
		sName = oSheetName.Text &amp; &quot;_&quot; &amp; IOtypes(i)
		If SheetExists(sName) Then
			ShExists = True
			oNameExistsLabel.Visible = True
			If oOWCheckBox.State = 0 Then oOKbutt.Enable = False
		End If
	Next i
	Select Case oEvent.source.getModel().Name
		Case &quot;DICheckBox&quot;, &quot;AICheckBox&quot;, &quot;DOCheckBox&quot;, &quot;AOCheckBox&quot;
			nCheck = 0 &apos;Count selected nodes
			For i = 0 To 3
				If oCheckBox(i).State = 1 Then nCheck = nCheck + 1
			Next i
			If nCheck &lt; 1 Then
				oOKbutt.Enable = False &apos;None of IO nodes selected
&apos;				MsgBox &quot;None of IO nodes selected&quot;
			Else
				If (NOT ShExists) Then
&apos;					MsgBox &quot;Sheet doesn&apos;t exist&quot;
					oOKbutt.Enable = True &apos;One or more of IO nodes selected
				ElseIf (ShExists AND oOWCheckBox.State = 1) Then
&apos;					MsgBox &quot;Sheet exists AND oOWCheckBox.State=1&quot;
					oOKbutt.Enable = True
				End If
			End If

		Case &quot;SheetPrefix&quot;
			oNameExistsLabel.Visible = False
			oOWCheckBox.Enable = False
			oOWCheckBox.State = 0
			If nCheck &gt; 0 Then oOKbutt.Enable = True
			For i = 0 To 3
				sName = oSheetName.Text &amp; &quot;_&quot; &amp; IOtypes(i)
				oCheckBox(i).Label = sName &amp; &quot; - &quot; &amp; nIO(i) &amp; &quot; item(s)&quot;
				If SheetExists(sName) Then
					ShExists = True
					oNameExistsLabel.Visible = True
					oOWCheckBox.Enable = True
					oOWCheckBox.State = 0
					oOKbutt.Enable = False
				End If
			Next i

		Case &quot;OWCheckBox&quot;
			Overwrite = oOWCheckBox.State = 1
			If nCheck &gt; 0 Then oOKbutt.Enable = Overwrite
	End Select
	Overwrite = oOWCheckBox.State = 1
End Sub

REM Insert new name into Column array
Sub InsertCols(offs As Integer, ixIO As Integer, newName As String)
	Dim i as Integer
	Dim last as Integer
	Dim colNames as Variant
	colNames = ColumnNames(ixIO)
	last = UBound(colNames()) + 1
	ReDim Preserve colNames(last)

	if offs &lt; last Then &apos;Shift elements towards the end of the array
		For i = last to offs + 1 Step -1
			colNames(i) = colNames(i - 1)
		Next i
	End If
	colNames(offs) = newName
	ColumnNames(ixIO) = colNames
End Sub

REM Check if there is an attribute name that needs to be added to the Column array
Sub UpdateCols(attrList() As String, ixIO As Integer)
	Dim i As Integer, a As Integer
	Dim offset As Integer &apos;Index of the attribute name in array, -1 if not found
	Dim insAt As Integer &apos;Offset to insert a new element in Column array

	For a = 0 to UBound(attrList()) &apos;Check if attribute name exists in Column array
		offset = IndexInArray(attrList(a), ColumnNames(ixIO))
		If offset &lt; 0 Then &apos;Attribute not found
			insAt = UBound(ColumnNames(ixIO)) + 1 &apos;Set initial insert offset beyond the end of the array
			For i = a + 1 to UBound(attrList()) &apos;Check if the next attribute name exists in array
				offset = IndexInArray(attrList(i), ColumnNames(ixIO)) &apos;Set the insert offset before next found attribute
				If offset &gt;= 0 Then &apos;Attribute found
					insAt = offset
					Exit For
				End If
			Next i
			InsertCols(insAt, ixIO, attrList(a)) &apos;Get space for the new element
		End If
	Next a
End Sub

Sub CreateCols(cName As String, oAttList As Object)
	Dim i As Integer
	Dim ixIO As Integer

	ixIO = IndexInArray(cName, IOtypes())
	If ixIO &lt; 0 Then Exit Sub
	If oAttList.Length &gt; 0 Then
		Dim attrList(oAttList.Length - 1) As String
		nIO(ixIO) = nIO(ixIO) + 1
		For i = 0 to oAttList.Length - 1
			attrList(i) = oAttList.getNameByIndex(i)
		Next i
		If UBound(ColumnNames(ixIO)) &lt; 0 Then
			ColumnNames(ixIO) = attrList
		Else
			UpdateCols(attrList(), ixIO)
		End If
	End If
End Sub

REM Check if value is Integer and copy to Cell
Sub CopyValue(vValue As Variant, oCell as Object)
	Dim fval As Double
	On Local Error Goto copyString

	fval = CDbl(vValue)	&apos;Convert to Double, On Error handles overflow
	If fval = vValue Then
		oCell.Value = vValue
	Else
		copyString:
		oCell.String = vValue
	End If
End Sub

REM Populate a sheet row with IO node attribute values
Sub GetValues(sNode As String, oAttList As Object)
	Dim nAttrib As Integer &apos;Number of attributes of sNode
	Dim i As Integer, c As Integer
	Dim ixIO As Integer

	ixIO = IndexInArray(sNode, IOtypes())
	If ixIO &lt; 0 Then Exit Sub
	If IsNull(sheetsIO(ixIO)) Then Exit Sub

	nAttrib = oAttList.getLength()
	Dim Row(UBound(ColumnNames(ixIO)))

	For i = 0 to nAttrib - 1
		c = IndexInArray(oAttList.getNameByIndex(i), ColumnNames(ixIO))
		If c &gt;= 0 Then
			Row(c) = oAttList.getValueByIndex(i)
			If Row(c) = &quot;&quot; Then
				If GemptyPlhdr &gt; 0 Then
					Row(c) = EMPTYKEY
				End If
			End If
		Else &apos;Attribute name not found in Columns array, this is not supposed to happen
			MsgBox &quot;Attribute: &quot; &amp; oAttList.getNameByIndex(i) &amp; &quot; is not found in Columns()&quot;, MB_ICONSTOP, MNAME
		End If
	Next i
	For c = 0 To UBound(Row())
		If Not IsEmpty(Row(c)) Then
			CopyValue(Row(c), sheetsIO(ixIO).getCellByPosition(c + GfirstCol - 1, Rpos(ixIO)))
		End If
	Next c
	Rpos(ixIO) = Rpos(ixIO) + 1
End Sub

Sub ReadXmlFromInputStream(oInputStream)
	Dim oSaxParser As Object
	Dim oDocEventsHandler As Object
	Dim oInputSource As Object
	&apos; Create a Sax Xml parser.
	oSaxParser = createUnoService(&quot;com.sun.star.xml.sax.Parser&quot;)
	&apos; Create a document event handler object.
	&apos; As methods of this object are called, Basic arranges
	&apos; for global routines (see below) to be called.
	oDocEventsHandler = CreateDocumentHandler()
	&apos; Plug our event handler into the parser.
	&apos; As the parser reads an Xml document, it calls methods
	&apos; of the object, and hence global subroutines below
	&apos; to notify them of what it is seeing within the Xml document.
	oSaxParser.setDocumentHandler(oDocEventsHandler)
	&apos; Create an InputSource structure.
	oInputSource = createUnoStruct(&quot;com.sun.star.xml.sax.InputSource&quot;)
	oInputSource.aInputStream = oInputStream &apos; plug in the input stream

	&apos; Now parse the document.
	&apos; This reads in the entire document.
	&apos; Methods of the oDocEventsHandler object are called as
	&apos; the document is scanned.
	oSaxParser.parseStream(oInputSource)
End Sub

Sub ReadXmlFromUrl(fName)
	Dim oInputStream As Object
	oInputStream = oSimpleFileAccess.openFileRead(fName)
	ReadXmlFromInputStream(oInputStream)
	oInputStream.closeInput()
End Sub

&apos;==================================================
&apos;  Xml Sax document handler.
&apos;==================================================
&apos; Global variables used by our document handler.
&apos;
&apos; Once the Sax parser has given us a document locator,
&apos; the glLocatorSet variable is set to True,
&apos; and the goLocator contains the locator object.
&apos;
&apos; The methods of the locator object has cool methods
&apos; which can tell you where within the current Xml document
&apos; being parsed that the current Sax event occured.
&apos; The locator object implements com.sun.star.xml.sax.XLocator.
&apos;
Private goLocator As Object
Private glLocatorSet As Boolean
	&apos; This creates an object which implements the interface
	&apos; com.sun.star.xml.sax.XDocumentHandler.
	&apos; The doucment handler is returned as the function result.
Function CreateDocumentHandler()
	&apos; Use the CreateUnoListener function of Basic.
	&apos; Basic creates and returns an object that implements a particular interface.
	&apos; When methods of that object are called,
	&apos; Basic will call global Basic functions whose names are the same
	&apos; as the methods, but prefixed with a certian prefix.
	Dim oDocHandler As Object
	oDocHandler = CreateUnoListener(&quot;DocHandler_&quot;, &quot;com.sun.star.xml.sax.XDocumentHandler&quot;)
	glLocatorSet = False
	CreateDocumentHandler() = oDocHandler
End Function

&apos;==================================================
&apos;  Methods of our document handler call these
&apos;  global functions.
&apos;  These methods look strangely similar to
&apos;  a SAX event handler. ;-)
&apos;  These global routines are called by the Sax parser
&apos;  as it reads in an XML document.
&apos;  These subroutines must be named with a prefix that is
&apos;  followed by the event name of the com.sun.star.xml.sax.XDocumentHandler interface.
&apos;==================================================
Function DocHandler_startDocument()
End Function

Function DocHandler_endDocument()
	ColumnsOK = True
End Function

Function DocHandler_startElement(sName As String, oAttList As com.sun.star.xml.sax.XAttributeList)
	If ColumnsOK Then
		GetValues(sName, oAttList)
	Else
		CreateCols(sName, oAttList)
	End If
End Function

Function DocHandler_endElement(sName As String)
End Function

Function DocHandler_characters(sChars As String)
End Function

Function DocHandler_ignorableWhitespace(sWhitespace As String)
End Function

Function DocHandler_processingInstruction(cTarget As String, cData As String)
End Function

Function DocHandler_setDocumentLocator(oLocator As com.sun.star.xml.sax.XLocator)
	&apos; Save the locator object in a global variable.
	&apos; The locator object has valuable methods that we can
	&apos; call to determine
	goLocator = oLocator
	glLocatorSet = True
End Function

Sub Main
	Dim fName As String &apos;XML file name
	Dim i As Integer, t As Integer
	Dim oModule, oDlg &apos;Dialog objects
&apos;	Dim oSheetName &apos;Sheet name prefix
	Dim oOptionButton1, oOptionButton2
	Dim oCell &apos;Cell object
	Dim oRange
	Dim clear As Boolean

	GlobalInit()
	IOtypes = Array(&quot;DI&quot;, &quot;AI&quot;, &quot;DO&quot;, &quot;AO&quot;)
	ColumnNames = Array(Array(), Array(), Array(), Array())
	oSheetObj = ThisComponent
	If GhomeURL = &quot;&quot; Then ResolveFilepath(&quot;&quot;)

	fName = OpenFile()
	If fName = &quot;&quot; Then
&apos;		MsgBox &quot;No file selected&quot;, MB_ICONINFORMATION, MNAME
		Exit Sub
	End If

	XMLn = FileNameoutofPath(fName)
	ReadXmlFromUrl(fName) &apos;First run to get column headers (attributes of the IO nodes)

	DialogLibraries.LoadLibrary(&quot;XML_lib&quot;)
	oDialogLib = DialogLibraries.getByName(&quot;XML_lib&quot;)
	oModule = oDialogLib.getByName(&quot;ImportDialog&quot;)
	oDlg = CreateUnoDialog(oModule)
	oSheetName = oDlg.GetControl(&quot;SheetPrefix&quot;)
	oCheckBox(0) = oDlg.GetControl(&quot;DICheckBox&quot;)
	oCheckBox(1) = oDlg.GetControl(&quot;AICheckBox&quot;)
	oCheckBox(2) = oDlg.GetControl(&quot;DOCheckBox&quot;)
	oCheckBox(3) = oDlg.GetControl(&quot;AOCheckBox&quot;)
	oNameExistsLabel = oDlg.GetControl(&quot;NameExists&quot;)
	oOWCheckBox = oDlg.GetControl(&quot;OWCheckBox&quot;)
	oOKbutt = oDlg.GetControl(&quot;ImportButt&quot;)
	oDlg.Title = XMLn &amp; &quot; - &quot; &amp; MNAME
	DialogSetPosition(oDlg, ThisComponent.getCurrentController().Frame.getComponentWindow(), 0)
	oSheetName.Text = GetFileNameWithoutExtension(XMLn)
	oNameExistsLabel.Visible = False
	oOWCheckBox.Enable = False
	oOKbutt.Enable = True
	For t = 0 to 3
		REM Update labels of checkboxes according the sheet name prefix (default -- XML file name wo extension)
		oCheckBox(t).Label = oSheetName.Text &amp; &quot;_&quot; &amp; IOtypes(t) &amp; &quot; - &quot; &amp; nIO(t) &amp; &quot; item(s)&quot;
		If nIO(t) &gt; 0 Then &apos;Set checkboxes for non-empty IO nodes
			oCheckBox(t).State = 1
		Else
			oCheckBox(t).Enable = False
		End If
		If SheetExists(oSheetName.Text &amp; &quot;_&quot; &amp; IOtypes(t)) Then
			oNameExistsLabel.Visible = True
			oOWCheckBox.Enable = True
			oOWCheckBox.State = 0
			oOKbutt.Enable = False
		End If
	Next t

	If oDlg.execute = 0 Then
&apos;		MsgBox &quot;Operation canceled&quot;, MB_ICONINFORMATION, MNAME
		Exit Sub
	End If

	For t = 0 To 3
		If oCheckBox(t).State = 1 Then
			Rpos(t) = 1
			If SheetExists(oSheetName.Text &amp; &quot;_&quot; &amp; IOtypes(t)) Then
				clear = True
			Else
				oSheetObj.Sheets.insertNewByName(oSheetName.Text &amp; &quot;_&quot; &amp; IOtypes(t), oSheetObj.Sheets.Count + 1)  &apos;Add new sheet at the end of the document
				clear = False
			End If
			sheetsIO(t) = oSheetObj.Sheets.getByName(oSheetName.Text &amp; &quot;_&quot; &amp; IOtypes(t))

			If clear Then  &apos;Clear all cell contents
				sheetsIO(t).ClearContents(511)	&apos;com.sun.star.sheet.CellFlags
			End If

			For i = 0 To UBound(ColumnNames(t))
				oCell = sheetsIO(t).getCellByPosition(i + GfirstCol - 1, 0) &apos;Start from B1
				oCell.String = ColumnNames(t)(i)
				&apos;oCell.CharWeight = com.sun.star.awt.FontWeight.BOLD
			Next i
		End If
	Next t

	oSheetObj.lockControllers()
	&apos;oSheetObj.enableAutomaticCalculation(False)
	ReadXmlFromUrl(fName) &apos;Second run to fill column data (values of the IO node attributes)
	&apos;Set optimal width of the columns
	For t = 3 To 0 Step -1 &apos;Process IO sheets in reverse direction to finish on the first used sheet
		If Not IsNull(sheetsIO(t)) Then
			oRange = sheetsIO(t).getCellRangeByPosition(1, 0, UBound(ColumnNames(t)) + 1, 0)
			oRange.Columns.OptimalWidth = True
			oSheetObj.CurrentController.ActiveSheet = sheetsIO(t)
			oSheetObj.CurrentController.freezeAtPosition(0, 1) &apos;Freeze the first row
		End If
	Next t
	oSheetObj.unlockControllers()
	&apos;oSheetObj.enableAutomaticCalculation(True)
	MsgBox &quot;Import complete!&quot; &amp; CHR$(13) &amp; ConvertFromURL(fName), MB_ICONINFORMATION, MNAME
End Sub

</script:module>